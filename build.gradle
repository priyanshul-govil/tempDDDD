//file:noinspection SpellCheckingInspection
//file:noinspection GroovyAssignabilityCheck

buildscript {  // for "com.github.ben-manes.versions" plugin
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.43.0"
    }
}

apply plugin: "com.github.ben-manes.versions"   //  ./gradlew dependencyUpdates
apply plugin: 'groovy'
apply plugin: 'java'

group = 'cz.siret'
version = '2.4.1-beta.2'


description = 'Ligand binding site prediction based on machine learning.'


allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases/" }
    flatDir(dirs: 'lib')
}

sourceSets {
    main {
        resources {
            exclude 'scripts/out/*'
            exclude 'models/*'
        }
    }
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            "project_version": project.version
    ]
}

task copyDependenciesToDist( type: Copy ) {
    into "$buildDir/../distro/bin/lib"
    from configurations.runtimeClasspath

}

task copyBinaryToDist( type: Copy ) {
    dependsOn jar

    into "$buildDir/../distro/bin"
    from "$buildDir/bin"
    include "*.jar"
}

task copyDocumentation( type: Copy ) {
    into "$buildDir/../distro/"
    from "$buildDir/../"
    include "README.md", "LICENSE.txt"
}

assemble {
    dependsOn processResources
    dependsOn copyBinaryToDist
    dependsOn copyDependenciesToDist // copy dependencies to distro dir
    dependsOn copyDocumentation
}

jar {
    archiveFileName = "p2rank.jar"
    destinationDirectory = file("build/bin")

    manifest {
        attributes 'Main-Class': 'cz.siret.prank.program.Main'
    }
}

clean {
    delete "$buildDir/../distro/bin"
    delete "$buildDir/../distro/test_output"
    delete "$buildDir/../distro/log/prank.log"
}

test {

    // show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2g"
    
    def distroDir = "$project.projectDir/distro"
    inputs.dir "$distroDir/test_data"
    inputs.files("$distroDir/models", "$distroDir/config/default.groovy")

    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    testLogging {
        events "failed" // "standardOut", "standardError", "passed", "skipped"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }

    // listen to standard out and standard error of the test JVM(s)
    //    onOutput { descriptor, event ->
    //        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    //    }
    
}

configurations {
    runtimeClasspath.extendsFrom implementation
}


dependencies {

    implementation 'org.apache.groovy:groovy:4.0.4'

    implementation 'org.codehaus.gpars:gpars:1.2.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.univocity:univocity-parsers:2.9.1' // csv parser
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation 'org.zeroturnaround:zt-zip:1.15'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.tukaani:xz:1.9'
    implementation 'com.github.luben:zstd-jni:1.5.2-4'
    implementation 'com.github.dpaukov:combinatoricslib3:3.3.3'

    implementation 'org.slf4j:slf4j-api:2.0.3'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
    implementation 'org.slf4j:jul-to-slf4j:2.0.3'         // for netlib logging messages

    implementation 'org.biojava:biojava-core:6.0.5'
    implementation 'org.biojava:biojava-alignment:6.0.5'
    implementation 'org.biojava:biojava-structure:6.0.5'

    implementation 'org.openscience.cdk:cdk-qsarmolecular:2.8'    // for NumericalSurface class
    implementation 'org.openscience.cdk:cdk-data:2.8'             // for Atom class

    implementation 'nz.ac.waikato.cms.weka:weka-dev:3.9.6'

    implementation fileTree(dir: 'lib', include: '*.jar')

    testImplementation 'junit:junit:4.13.2'

}
